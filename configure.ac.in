dnl configure.ac
dnl
dnl xfce4-xkb-plugin - XKB plugin for xfce4-panel
dnl
dnl 2003 Benedikt Meurer <benedikt.meurer@unix-ag.uni-siegen.de>
dnl

AC_CONFIG_MACRO_DIRS([m4])

dnl ***************************
dnl *** Version information ***
dnl ***************************
m4_define([xkb_version_major], [0])
m4_define([xkb_version_minor], [8])
m4_define([xkb_version_micro], [2])
m4_define([xkb_version_nano], [])
m4_define([xkb_version_build], [@REVISION@])
m4_define([xkb_version_tag], [git])
m4_define([xkb_version], [xkb_version_major().xkb_version_minor().xkb_version_micro()ifelse(xkb_version_nano(), [], [], [.xkb_version_nano()])ifelse(xkb_version_tag(), [git], [xkb_version_tag()-xkb_version_build()], [xkb_version_tag()])])

dnl *******************************************
dnl *** Debugging support for GIT  snapshots ***
dnl *******************************************
m4_define([xkb_debug_default], [ifelse(xkb_version_tag(), [git], [yes], [minimum])])


dnl ***************************
dnl *** Initialize autoconf ***
dnl ***************************
AC_COPYRIGHT([Copyright (c) 2006-2022
        The Xfce development team. All rights reserved.])
AC_INIT([xfce4-xkb-plugin], [xkb_version], [https://gitlab.xfce.org/panel-plugins/xfce4-xkb-plugin/], [xfce4-xkb-plugin])
AC_PREREQ([2.50])
AC_CANONICAL_TARGET()
AC_REVISION([@REVISION@])

dnl ***************************
dnl *** Initialize automake ***
dnl ***************************
AM_INIT_AUTOMAKE([1.8 dist-bzip2 no-dist-gzip tar-ustar foreign])
AC_CONFIG_HEADERS([config.h])
AM_MAINTAINER_MODE()
m4_ifdef([AM_SILENT_RULES], [AM_SILENT_RULES([yes])])

dnl *******************************
dnl *** Check for UNIX variants ***
dnl *******************************
AC_USE_SYSTEM_EXTENSIONS

dnl ********************************
dnl *** Check for basic programs ***
dnl ********************************
AC_PROG_CC()
LT_PATH_LD([])
AC_PROG_INSTALL()
IT_PROG_INTLTOOL([0.35.0])
LT_PREREQ([2.2.6])
LT_INIT([disable-static])
AM_PROG_CC_C_O()

dnl *********************
dnl *** Check for X11 ***
dnl *********************
XDT_CHECK_LIBX11_REQUIRE()

dnl ******************************
dnl *** Check for i18n support ***
dnl ******************************
XDT_I18N([@LINGUAS@])

dnl ***********************************
dnl *** Check for required packages ***
dnl ***********************************
XDT_CHECK_PACKAGE([GTK], [gtk+-3.0], [3.20.0])
XDT_CHECK_PACKAGE([LIBXFCE4PANEL], [libxfce4panel-2.0], [4.12.0])
XDT_CHECK_PACKAGE([LIBXFCE4UTIL], [libxfce4util-1.0], [4.12.0])
XDT_CHECK_PACKAGE([LIBXFCE4UI], [libxfce4ui-2], [4.12.0])
XDT_CHECK_PACKAGE([XFCONF], [libxfconf-0], [4.12.1])
XDT_CHECK_PACKAGE([LIBXKLAVIER], [libxklavier], [5.3])
XDT_CHECK_PACKAGE([LIBRSVG], [librsvg-2.0], [2.40])
XDT_CHECK_PACKAGE([LIBWNCK], [libwnck-3.0], [3.14])
XDT_CHECK_PACKAGE([GARCON], [garcon-1], [0.4.0])

dnl **********************************
dnl *** Optional libnotify Support ***
dnl **********************************
XDT_CHECK_OPTIONAL_PACKAGE([LIBNOTIFY], [libnotify], [0.7.0], [libnotify],
                           [libnotify library])

dnl ***********************************
dnl *** Check for debugging support ***
dnl ***********************************
XDT_FEATURE_DEBUG([xkb_debug_default])

AC_CONFIG_FILES([
panel-plugin/Makefile
flags/Makefile
Makefile
po/Makefile.in
icons/Makefile
icons/16x16/Makefile
icons/32x32/Makefile
icons/128x128/Makefile
icons/scalable/Makefile
])
AC_OUTPUT

dnl ***************************
dnl *** Print configuration ***
dnl ***************************
echo
echo "Build Configuration:"
echo
echo "* Debug Support:      $enable_debug"
echo "* libnotify Support:  ${LIBNOTIFY_FOUND:-no}"
echo
